# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgbase="${MINGW_PACKAGE_PREFIX}-${pkgname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code"
url='https://github.com/VcDevel/Vc'
arch=(x86_64)
license=(LGPL3)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/VcDevel/Vc/releases/download/$pkgver/Vc-$pkgver.tar.gz")
sha256sums=('08c629d2e14bfb8e4f1a10f09535e4a3c755292503c971ab46637d2986bdb4fe')

prepare() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"

# Fix i686 build
  cd Vc-${pkgver}
  sed -e 's:(x86|AMD64):(i686|x86|AMD64):g' -i CMakeLists.txt
}

build() {
  cd "${srcdir}/build-${MINGW_CHOST}"

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    ../Vc-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_SH="CMAKE_SH-NOTFOUND" \
    -DCMAKE_BUILD_TYPE=Release \
    -DTARGET_ARCHITECTURE=generic \

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
